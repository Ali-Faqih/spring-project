buildscript {
    repositories {
        mavenRepo(urls: 'http://repo.gradle.org/gradle/plugins-snapshots').setChecksums('sha1')
    }
    dependencies {
        classpath 'org.gradle.plugins:gradle-signing-plugin:0.0.1-SNAPSHOT'
    }
}

allprojects {
    group = 'org.springframework.project.gradle'
    version = '1.0.2.BUILD-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'bundlor'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    xLintArg = '-Xlint:all'
    [compileJava, compileTestJava]*.options*.compilerArgs = [xLintArg]

    repositories {
        mavenRepo urls: 'https://internal-repo.springsource.org/libs-snapshot'
    }

    dependencies {
        compile 'log4j:log4j:1.2.16'
        testCompile 'junit:junit:4.8.2'
    }

    bundlor {
        enabled = true
        failOnWarnings = true
        bundleManifestVersion = 2
        bundleVendor = 'SpringSource'
        bundleName = project.description
        bundleVersion = project.version
        importTemplate = [
            'org.apache.log4j.*;version="[1.2.16, 2.0.0)"'
        ]
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    signing {
        sign configurations.archives
    }

    configurations {
        published.extendsFrom archives, signatures
    }

    /* not available until upgrading to m5
    uploadPublished {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signPom(deployment.pomArtifact) }
            }
        }
    }
    */
}

project(':spring-project-core') {
    description = "Spring Project (Gradle) Core"
    bundlor {
        bundleSymbolicName = 'org.springframework.project.core'
    }
}

project(':spring-project-aj') {
    description = "Spring Project (Gradle) AspectJ"
    bundlor {
        bundleSymbolicName = 'org.springframework.project.aj'
        importTemplate += [
            "org.springframework.project.core.*;version=\"${project.version}\""
        ]
    }
    dependencies {
        compile project(':spring-project-core')
    }
}
